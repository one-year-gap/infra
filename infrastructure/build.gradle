plugins {
    id 'java'
    id 'application'
    id 'jacoco'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

group = 'com.myorg'
version = '0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'software.amazon.awscdk:aws-cdk-lib:2.238.0'
    implementation 'software.constructs:constructs:10.4.2'
    testImplementation platform('org.junit:junit-bom:5.12.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core:3.27.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation "com.github.stefanbirkner:system-lambda:1.2.1"
}

application {
    mainClass = 'com.myorg.InfrastructureApp'
}

test {
    useJUnitPlatform()
    finalizedBy tasks.named("jacocoTestReport")
    environment "API_SERVER_REPOSITORY", "one-year-gap/api-server"
    environment "ADMIN_WEB_REPOSITORY", "one-year-gap/admin-web"
}

jacoco {
    toolVersion = "0.8.14"
}

def jacocoExcludes = [
        // AWS CDK entrypoint/bootstrap classes
        '**/*App*',
        '**/InfrastructureApp*',

        // Configuration package
        '**/config/**',
        '**/*Config*',
        '**/*Configuration*',
]

tasks.named("jacocoTestReport", JacocoReport) {
    dependsOn tasks.named("test")

    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }

    classDirectories.setFrom(
            files(classDirectories.files.collect { dir ->
                fileTree(dir: dir, exclude: jacocoExcludes)
            })
    )
}

tasks.named("jacocoTestCoverageVerification", JacocoCoverageVerification) {
    enabled = false

    classDirectories.setFrom(
            files(classDirectories.files.collect { dir ->
                fileTree(dir: dir, exclude: jacocoExcludes)
            })
    )

    violationRules {
        rule {
            enabled = true
            element = 'BUNDLE'
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.7
            }
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.7
            }
        }
    }
}
